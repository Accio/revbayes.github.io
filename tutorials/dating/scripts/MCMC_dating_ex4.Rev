########################################################
#
# RevBayes Example: Molecular dating
# 
# This file: Runs the full MCMC for exercise 4
#
# authors: Rachel C. M. Warnock, Sebastian Hoehna
#
########################################################

##################################################################################
#
# Exercise 4: Estimating speciation times using the fossilized birth-death process
#
##################################################################################

#######################
# Reading in the Data #
#######################

# Create the full list of taxa (including all fossils and extant species) #
taxa <- readTaxonData("data/bears_taxa.tsv")

# Import the molecular sequences #
# this file contains only the taxa for which sequence data are available #
cytb <- readDiscreteCharacterData("data/bears_cytb.nex")

# Add the missing taxa to each data partition #
cytb.addMissingTaxa( taxa )

## helpers
n_taxa <- cytb.size()

mvi = 1

# Load the model files

source("scripts/tree_FBD.Rev") # BD tree prior + node calibrations

source("scripts/clock_relaxed.Rev") # Global clock model

source("scripts/sub_GTRG.Rev") # Molecular substitution model (GTR+G)

########
# MCMC #
########

# initialize the model object #
mymodel = model(sf)	

mni = 1

# Create a vector of monitors #
# 1. for the full model #
monitors[mni++] = mnModel(filename="output/bears_FBD.log", printgen=10)	

# 2. the tree #
monitors[mni++] = mnFile(filename="output/bears_FBD.trees", printgen=10, pruned_tree)

# 3. and a few select parameters to be printed to the screen #
monitors[mni++] = mnScreen(printgen=10, extant_mrca, num_samp_anc, diversification)

# Initialize the MCMC object #
mymcmc = mcmc(mymodel, monitors, moves)

# Run the MCMC #
mymcmc.run(generations=20000)

########################
# Summarizing the tree #
########################

# Read the trace file #
trace = readTreeTrace("output/bears_FBD.trees")

# Maximum clade credibility tree #
mccTree(trace, file="output/bears_FBD.mcc.tre" )

# Quit RevBayes #
q()
