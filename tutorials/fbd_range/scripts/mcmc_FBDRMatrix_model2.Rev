# read stratigraphic ranges
taxa = readTaxonData(file = "data/dinosaur_ranges.tsv")

# interval boundaries
timeline <- v(100, 145, 201) - 66

mvi = 1

alpha <- 10

# specify FBDR model parameters
for(i in 1:(timeline.size()+1))
{

	mu[i] ~ dnExp(alpha)
	lambda[i] ~ dnExp(alpha)
	psi[i] ~ dnExp(alpha)
	
	div[i] := lambda[i] - mu[i]
	turnover[i] := mu[i]/lambda[i]

	moves[mvi++] = mvScale(mu[i], lambda = 0.01)
	moves[mvi++] = mvScale(mu[i], lambda = 0.1)
	moves[mvi++] = mvScale(mu[i], lambda = 1)

	moves[mvi++] = mvScale(lambda[i], lambda = 0.01)
	moves[mvi++] = mvScale(lambda[i], lambda = 0.1)
	moves[mvi++] = mvScale(lambda[i], lambda = 1)

	moves[mvi++] = mvScale(psi[i], lambda = 0.01)
	moves[mvi++] = mvScale(psi[i], lambda = 0.1)
	moves[mvi++] = mvScale(psi[i], lambda = 1)
}

rho <- 0

# model 2
bd ~ dnFBDRMatrix(taxa=taxa, lambda=lambda, mu=mu, psi=psi, rho=rho, timeline=timeline)

mymodel = model(alpha)

# add monitors
mni = 1

monitors[mni++] = mnScreen(lambda, mu, psi, printgen=100)
monitors[mni++] = mnModel(filename="output/model2.log",printgen=10)

# monitors to print RevGagets input
monitors[mni++] = mnFile(filename="output/model2_speciation_rates.log",lambda,printgen=10)
monitors[mni++] = mnFile(filename="output/model2_speciation_times.log",timeline,printgen=10)
monitors[mni++] = mnFile(filename="output/model2_extinction_rates.log",mu,printgen=10)
monitors[mni++] = mnFile(filename="output/model2_extinction_times.log",timeline,printgen=10)
monitors[mni++] = mnFile(filename="output/model2_sampling_rates.log",psi,printgen=10)
monitors[mni++] = mnFile(filename="output/model2_sampling_times.log",timeline,printgen=10)

# run the analysis
mymcmc = mcmc(mymodel, moves, monitors, moveschedule="random")
mymcmc.run(30000)

q()
