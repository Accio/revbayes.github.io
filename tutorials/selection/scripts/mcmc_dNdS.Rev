################################################################################
#
# RevBayes Analysis: Bayesian inference of dN/dS rates.
#                    
# 
#
# authors: Sebastian Hoehna
#
################################################################################

#######################
# Reading in the Data #
#######################

###### This just defines a single model for all sites #######

### Read in sequence data for both genes

data = readDiscreteCharacterData("data/Group_1.fasta")
data_codon = data.translateCharacters("Codon")

# Get some useful variables from the data. We need these later on.
n_species <- data.ntaxa()
taxa <- data.taxa()

num_data_subsets = data.size()

# set my move index
mvi = 0
mni = 0



##############
# Tree model #
##############

# work under fixed topology
psi <- readTrees("data/5sp_Heliconius_Kozak_2015.nex")[1]


######################
# Substitution Model #
######################

# sigma: variance per unit of time of the Brownian motion
omega ~ dnLognormal(0,1)
moves[++mvi] = mvSlide(omega, delta=1.0, tune=true, weight=2.0)

kappa ~ dnLognormal(0,1)
moves[++mvi] = mvSlide(kappa, delta=1.0, tune=true, weight=2.0)

pi ~ dnDirichlet( rep(1,4) )
moves[++mvi] = mvBetaSimplex(pi, alpha=1.0, tune=true, weight=2.0)
moves[++mvi] = mvDirichletSimplex(pi, alpha=1.0, tune=true, weight=2.0)

Q := fnCodonHKY( omega, kappa, pi )
#Q := fnCodon( omega, pi )


# the sequence evolution model
seq ~ dnPhyloCTMC(tree=psi, Q=Q, type="Codon")

# attach the data
seq.clamp(data_codon)





#############
# THE Model #
#############

# We define our model.
# We can use any node of our model as a handle, here we chose to use the rate matrix.
mymodel = model(psi)



monitors[++mni] = mnModel(filename="output/dNdS.log",printgen=10, separator = TAB)
monitors[++mni] = mnScreen(printgen=100, omega, kappa, pi)

#mymcmc = mcmcmc(mymodel, monitors, moves, nchain=4, nruns=10)
mymcmc = mcmc(mymodel, monitors, moves, nruns=1)

mymcmc.burnin(generations=1000,tuningInterval=200)
mymcmc.run(generations=3000)


# you may want to quit RevBayes now
q()

